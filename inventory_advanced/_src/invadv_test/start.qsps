QSP-Game start

# start
! BASE
*p 'Простой пример для демонстрации возможностей библиотечки для работы с инвентарем (inventory_advanced.qsp).

Пример намеренно сделан очень просто, хотя некоторые функции напрашивались, например вывод действий по выбранному предмету.

Вы можете добавлять и удалять предметы (деньги или еще что-то), при этом их количество будет верно отображаться в инвентаре, заботу об этом берет библиотечка.

Вы можете добавлять предметы в обход функций библиотечки, они не помешают её работе.

Потыкайте по кнопкам и проверьте её работу, затем обязательно посмотрите код, вы оцените что он стал проще и нагляднее.

Ntropy (...@yandex.ru) 18.06.2009
А можно личкой на форуме http://qsp.borda.ru

'
! END BASE
!Загружаем библиотечку как простой список локаций
inclib 'inventory_advanced.qsp'

!Добавляем одну денежку и сразу ставим что считаем их в рублях
@ia_add_obj('деньги', 1, 'руб.')
!Добавляем шишку а тип пока не задаем
@ia_add_obj('шишка')
!задаем тип, что это шишки считаем в штуках
@ia_obj_type('шишка', 'шт.')
!Выводим статус ...
@status
!... и описание
'В руках 1 рубль и 1 шишка'

!Добавляем обработку для всех возможных операций
act 'добавить деньгу':
	'Взяли 1 руб'
	@ia_add_obj('деньги')
	@status
end

act 'убрать деньгу':
	if func('ia_obj_count', 'деньги') > 0:
		'Отдали 1 руб'
		@ia_del_obj('деньги')
		@status
	else
		'А деньги кончились'
	end
end

act 'раздать все деньги':
	if func('ia_obj_count', 'деньги') > 0:
		'Отдали все'
		@ia_obj_count('деньги', 0)
		@status
	else
		'А нету'
	end
end

act 'собрать 10 рублей':
	'Собрали 10 руб'
	@ia_obj_count('деньги', 10)
	@status
end

act 'поднять шишку':
	'Подняли шишку'
	@ia_add_obj('шишка')
	@status
end

act 'выбросить шишку':
	if func('ia_obj_count', 'шишка') > 0:
		'Выбросили шишку'
		@ia_del_obj('шишка')
		@status
	else
		'Шишки нету'
	end
end

act 'набрать 10 шишек':
	'Насобирали 10 шишек'
	@ia_obj_count('шишка', 10)
	@status
end

act 'высыпать все шишки':
	if func('ia_obj_count', 'шишка') > 0:
		'Разбросали все шишки'
		@ia_obj_count('шишка', 0)
		@status
	else
		'А кончились'
	end
end

!Добавляем предмет для классической обработки
$гвозди = 1
addobj "гвоздь (<<$гвозди>>)"
'плюс 1 гвоздь'
@status

act "вытащить гвоздь":
	'Выдернули гвоздь'
	delobj "гвоздь (<<$гвозди>>)"
	гвозди += 1
	addobj "гвоздь (<<$гвозди>>)"
	@status
end

act "забить гвоздь":
	'Забили гвоздь в доску'
	if гвозди > 0:
		delobj "гвоздь (<<$гвозди>>)"
		гвозди -= 1
		addobj "гвоздь (<<$гвозди>>)"
		@status
	end
end
-- start ---------------------------------